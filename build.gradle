apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'com.github.mouse0w0'
version '1.2.0'

sourceCompatibility = targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.1'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = "I18n"
                description = "An i18n library for Java application."
                url = "https://github.com/mouse0w0/i18n"
                licenses {
                    license {
                        name = "BSD 3-Clause License"
                        url = "https://opensource.org/licenses/BSD-3-Clause"
                    }
                }
                developers {
                    developer {
                        name = "Mouse"
                        email = "mouse0w0@qq.com"
                    }
                }
                scm {
                    url = "https://github.com/mouse0w0/i18n.git"
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty("ossUsername") ?: System.getenv("USERNAME")
                password = project.findProperty("ossPassword") ?: System.getenv("PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/mouse0w0/i18n")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}